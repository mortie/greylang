#!/usr/bin/env grey

// 'Range' class
Range := {

	// Constructor takes a min (inclusive) and max (exclusive)
	$init: {(min, max)
		self.min = min;
		self.max = max;
	},

	// $iter is run when we iterate over it
	$iter: {

		i := self.min;

		// Create a 'next' function, which will be called for each
		// iteration until it returns none
		next := {

			// If i < max, return and increment i,
			// otherwise return none to end the iteration
			if(i < self.max, {
				i = i + 1;
				i - 1;
			}, none);
		};

		// Return our object with $next set to our 'next'-function
		{ $next: next };
	}
};

{(val, key) print(key .. ": " .. val) } for Range(10, 20);
