#!/usr/bin/env grey

// Classes don't really exist, we have prototypes
Animal := {
	sound: "generic",
	talk: {
		print("I say", self.sound)
	},

	$init: {;}
};

// Dog looks like { sound: "Woof" }, with Animal as its prototype
Dog := extend(Animal, {
	sound: "Woof"
});

// Cat looks like { sound: "Meow" }, with Animal as its prototype
Cat := extend(Animal, {
	sound: "Meow"
});

// my-dog gets its talk property from Animal, and its sound property from Dog
my-dog := Dog();
my-dog.talk(); // "I say Woof"

// my-cat gets its talk property from Animal, and its sound property from Cat
my-cat := Cat();
my-cat.talk(); // "I say Meow"

// $init can also take arguments
Person := extend(Animal, {
	$init: {(name)
		self.sound = "Hello, my name is " .. name
	}
});

my-person := Person("Peter");
my-person.talk(); // "I say Hello, my name is Peter"
